plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
}

version = "${mod_v}"
group = "${mod_group}"
archivesBaseName = "${mod_name}-fabric-${mc_v}"

//repositories {
//    maven {
//        name = "Fuzs Mod Resources"
//        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
//    }
//}

dependencies {
    minecraft "com.mojang:minecraft:${mc_v}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_v}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_v}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":Common")

    modImplementation "curse.maven:modmenu-308702:3902967"
    modImplementation("net.minecraftforge:forgeconfigapiport-fabric:4.1.3")
}

loom {
    accessWidenerPath = file("src/main/resources/tbouncepad.accesswidener")

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", "${mod_v}"

    filesMatching("fabric.mod.json") {
        expand "version": "${mod_v}"
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
